apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'




android {
    compileSdkVersion 28

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 5
        versionName "5.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

ext.build = [
        mVersion : "0.0.8-beta",
        mUser  : "imanx",
        mToken : "",
]



version = build.mVersion
group = "com.zarinpal"




task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}



artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}




bintray {
    user = build.mUser
    key = build.mToken
    pkg {

        repo = 'purchase'
        name = 'pg'

        version {
            name = build.mVersion
            desc = 'ZarinPal In App Purchase SDK'
            released = new Date()
            vcsTag = build.mVersion
        }

        licenses = ['Apache-2.0']
        vcsUrl = ''
        websiteUrl = 'www.zarinpal.com'
    }
    configurations = ['archives']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    compileOnly 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.volley:volley:1.1.1'

}

